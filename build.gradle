buildscript {
    ext {
        kotlinVersion = "1.1.4-3"
        config4kVersion = "0.3.0"
        junitPlatformVersion = "1.0.0-M4"
        requeryVersion = "1.4.0"
        guiceVersion = "4.0"
        kotlinReflect = "1.1.0"
        grpcDeployables = [
                project(':ws-athletes'),
                project(':ws-countries'),
                project(':ws-events'),
                project(':ws-medals'),
                project(':ws-olympics')
        ]
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
    }
}

group 'hack-week-br'
version '1.0'

allprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
}

configure(grpcDeployables) {

    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    apply plugin: "idea"
    apply plugin: "application"
    apply plugin: "kotlin-kapt"

    mainClassName = "app.ServerKt"

    sourceCompatibility = 1.8

    dependencies {
        compile "io.github.config4k:config4k:$config4kVersion"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
        compile "com.google.inject:guice:$guiceVersion"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinReflect"
        compile "com.google.api.graphql:rejoiner:0.0.1-SNAPSHOT"
        compile "com.google.api.graphql:execution:0.0.1-SNAPSHOT"
        compile project(':common')
        kapt "io.requery:requery-processor:$requeryVersion"
    }

    sourceSets.main.java.srcDir file("${project.buildDir}/generated/source/kapt/main")
    sourceSets.test.java.srcDir file("${project.buildDir}/generated/source/kapt/test")

    idea {
        module {
            // Tell idea to mark the folder as generated sources
            generatedSourceDirs += file("$buildDir/generated/source/kapt/main")
            generatedSourceDirs += file("$buildDir/generated/source/kapt/test")
        }
    }

    kotlin { experimental { coroutines "enable" } }

    compileKotlin {
        kotlinOptions {
            apiVersion = "1.1"
            jvmTarget = "1.8"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}